import java.util.regex.Matcher

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation files("libs/craftbukkit-1060.jar")
}

File ymlFile = file('src/main/resources/plugin.yml') as File

if (!ymlFile.exists()) {
    throw new GradleException("The 'plugin.yml' file does not exist in 'src/main/resources'!")
}

String pluginVersion
String pluginName
String ymlText = ymlFile.text

Matcher mainMatcher = (ymlText =~ /main:\s*(\S+)/)
Matcher versionMatcher = (ymlText =~ /version:\s*(\S+)/)
Matcher nameMatcher = (ymlText =~ /name:\s*(\S+)/)

if (!mainMatcher.find()) {
    throw new GradleException("The 'main' attribute wasn't found in the 'plugin.yml' file!")
}

if (versionMatcher.find()) {
    pluginVersion = versionMatcher.group(1)
} else {
    throw new GradleException("The 'version' attribute wasn't found in the 'plugin.yml' file!")
}

if (nameMatcher.find()) {
    pluginName = nameMatcher.group(1)
} else {
    throw new GradleException("The 'name' attribute wasn't found in the 'plugin.yml' file!")
}

jar {
    manifest {
        attributes(
                'Implementation-Title': pluginName,
                'Implementation-Version': pluginVersion
        )
    }

    archiveVersion.set(pluginVersion)
    archiveBaseName.set(pluginName)
}
